export def main [] {
    let media = ls resources/font-atlas/*~144w.png | each {|file|
        let parsed_path = $file.name | path parse
        {
            type: bitmap,
            name: $"IOSEVKA_ATLAS_($parsed_path.stem | parse --regex "(?<num>[0-9]+).*" | get num | first | into int)",
            file: ($file.name | path split | slice 1.. | path join | str replace "~144w.png" ".png"),
            memoryFormat: 8Bit,
            spaceOptimization: memory,
        }
    }
    let bold_media = ls resources/font-atlas-bold/*~144w.png | each {|file|
        let parsed_path = $file.name | path parse
        {
            type: bitmap,
            name: $"IOSEVKA_ATLAS_BOLD_($parsed_path.stem | parse --regex "(?<num>[0-9]+).*" | get num | first | into int)",
            file: ($file.name | path split | slice 1.. | path join | str replace "~144w.png" ".png"),
            memoryFormat: 8Bit,
            spaceOptimization: memory,
        }
    }
    {
        name: lexiclock,
        author: fishy,
        version: "1.0.0",
        keywords: [ "pebble-watchface" ],
        private: true,
        dependencies: {},
        pebble: {
            displayName: lexiclock,
            uuid: 1c6a1f0b-8194-491f-acc4-fc06a6f9966d,
            sdkVersion: "3",
            enableMultiJS: true,
            targetPlatforms: [
#                 aplite,
                basalt,
#                 chalk,
#                 diorite,
                emery,
            ],
            watchapp: { watchface: true },
            messageKeys: [],
            resources: {
                media: ($media ++ $bold_media),
            },
        },
    }
}

export def json [] {
    main | to json
}

export def export [] {
    {
        # Add a little help message
        AUTOGENERATED: "Edit generate-package.nu and regenerate with `use generate-package.nu; generate-package export`"
    } | merge (main) | to json | save package.json --force
}
